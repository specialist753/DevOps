
&НаКлиенте
Процедура ТипСообщенияПриИзменении(Элемент)
	УстановитьПараметрыВыбораШаблонСообщений();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ОбъектРасчетов.Видимость = Параметры.РазовоеУведомление; 
	Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Запись.КлючРегламентногоЗадания = ""; 	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыВыбораШаблонСообщений()
	
	Если Не Запись.ТипСообщения.Пустая() тогда
		ПараметрыВыбора = Новый Массив;
		Если Запись.ТипСообщения = ПредопределенноеЗначение("Перечисление.Аид_ТипыСообщений.Email") тогда
			ПараметрыВыбора.Добавить(Новый ПараметрВыбора("ВидСообщения", "Письмо"));
		ИначеЕсли Запись.ТипСообщения = ПредопределенноеЗначение("Перечисление.Аид_ТипыСообщений.SMS") тогда
			ПараметрыВыбора.Добавить(Новый ПараметрВыбора("ВидСообщения", "СообщениеSMS"));
		ИначеЕсли Запись.ТипСообщения = ПредопределенноеЗначение("Перечисление.Аид_ТипыСообщений.ГолосовоеСообщение") тогда
			ПараметрыВыбора.Добавить(Новый ПараметрВыбора("ВидСообщения", "ГолосовоеСообщение"));
	    КонецЕсли;
		Элементы.ШаблонСообщения.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьПараметрыВыбораШаблонСообщений();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	//Проверяем на правильность заполнение времени, если установлен флаг УчитыватьЧасовойПояс
	Если ТекущийОбъект.УчитыватьЧасовойПояс Тогда
		РазницаЧасов = (ТекущийОбъект.ВремяОкончания - ТекущийОбъект.ВремяНачала)/3600;
		Если РазницаЧасов < 1 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("При установленном флаге ""Учитывать часовой пояс"" 
																|разница между временем окончания и начала должна быть не меньше часа.",,,,Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.КлючРегламентногоЗадания) Тогда	
		ТекущийОбъект.КлючРегламентногоЗадания = Новый УникальныйИдентификатор;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	//Создаем сразу регл. задание по правилу.
	МассивКлючей = Новый Массив;
	МассивКлючей.Добавить(ТекущийОбъект.КлючРегламентногоЗадания);
	Аид_ОбщегоНазначенияПереопределяемый.ЗапускРегламентныхЗаданийАИД(МассивКлючей);
	
КонецПроцедуры
