///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет состав назначений и общие реквизиты в шаблонах сообщений 
//
// Параметры:
//  Настройки - Структура:
//    * ПредметыШаблонов - ТаблицаЗначений - содержит варианты предметов для шаблонов. Колонки:
//         ** Имя           - Строка - уникальное имя назначения.
//         ** Представление - Строка - представление варианта.
//         ** Макет         - Строка - имя макета СКД, если состав реквизитов определяется посредством СКД.
//         ** ЗначенияПараметровСКД - Структура - значения параметров СКД для текущего предмета шаблона сообщения.
//    * ОбщиеРеквизиты - ДеревоЗначений - содержит описание общих реквизитов доступных во всех шаблонах. Колонки:
//         ** Имя            - Строка - уникальное имя общего реквизита.
//         ** Представление  - Строка - представление общего реквизита.
//         ** Тип            - Тип    - тип общего реквизита. По умолчанию строка.
//    * ИспользоватьПроизвольныеПараметры  - Булево - указывает, можно ли использовать произвольные пользовательские
//                                                    параметры в шаблонах сообщений.
//    * ЗначенияПараметровСКД - Структура - общие значения параметров СКД, для всех макетов, где состав реквизитов
//                                          определяется средствами СКД.
//
Процедура ПриОпределенииНастроек(Настройки) Экспорт
	
	//++ НЕ ГОСИС
	Настройки.ИспользоватьПроизвольныеПараметры = Истина;
	Настройки.ЗначенияПараметровСКД.Вставить("ОбращениеМужскойРод", НСтр("ru = 'Уважаемый';
																		|en = 'Dear'"));
	Настройки.ЗначенияПараметровСКД.Вставить("ОбращениеЖенскийРод", НСтр("ru = 'Уважаемая';
																		|en = 'Dear'"));
	Настройки.ЗначенияПараметровСКД.Вставить("ОбращениеПолНеопределен", НСтр("ru = 'Уважаемый(ая)';
																			|en = 'Dear'"));
	Настройки.ЗначенияПараметровСКД.Вставить("СтрокаБезОграничений", НСтр("ru = 'Без ограничений';
																			|en = 'Without restrictions'"));
	Настройки.ЗначенияПараметровСКД.Вставить("МожетВыкупатьсяЧастично", НСтр("ru = 'Может выкупаться частично';
																			|en = 'May be purchased in parts'"));
	Настройки.ЗначенияПараметровСКД.Вставить("НеМожетВыкупатьсяЧастично", НСтр("ru = 'Не может выкупаться частично';
																				|en = 'Cannot be purchased in parts'"));
	
	Для Каждого ЗначениеПеречисления ИЗ Перечисления.Аид_ТипыСобытийОповещений.ИзменениеСостоянияЗаказа.Метаданные().ЗначенияПеречисления Цикл
	
		Если НЕ Перечисления.Аид_ТипыСобытийОповещений.ДанныеДляОбработкиОчередиОповещений(Перечисления.Аид_ТипыСобытийОповещений[ЗначениеПеречисления.Имя]).ДоступноДляИспользования Тогда
			Продолжить;
		КонецЕсли;
		
		Предмет = Настройки.ПредметыШаблонов.Добавить();
		Предмет.Имя = ЗначениеПеречисления.Имя;
		Предмет.Представление = НСтр("ru = 'Оповещение клиента';
									|en = 'Notifications to customer'") + " """ + ЗначениеПеречисления.Синоним +"""";
		Предмет.Макет = ЗначениеПеречисления.Имя;
	
	КонецЦикла;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Вызывается при подготовке шаблонов сообщений и позволяет переопределить список реквизитов и вложений.
//
// Параметры:
//  Реквизиты - ДеревоЗначений - список реквизитов шаблона:
//    * Имя            - Строка - уникальное имя общего реквизита;
//    * Представление  - Строка - представление общего реквизита;
//    * Тип            - Тип    - тип реквизита. По умолчанию строка;
//    * Подсказка      - Строка - расширенная информация о реквизите;
//    * Формат         - Строка - формат вывода значения для чисел, дат, строк и булевых значений.
//  Вложения - ТаблицаЗначений - печатные формы и вложения, где:
//    * Имя           - Строка - уникальное имя вложения;
//    * Идентификатор - Строка - идентификатор вложения;
//    * Представление - Строка - представление варианта;
//    * Подсказка     - Строка - расширенная информация о вложении;
//    * ТипФайла      - Строка - тип вложения, который соответствует расширению файла: "pdf", "png", "jpg", mxl" и др.
//  НазначениеШаблона       - Строка  - имя назначения шаблон сообщения.
//  ДополнительныеПараметры - Структура - дополнительные сведения о шаблоне сообщения.
//
Процедура ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, НазначениеШаблона, ДополнительныеПараметры) Экспорт
	
	//++ НЕ ГОСИС
	МассивНазначенийШаблона = Новый Массив;
	Для Каждого ЗначениеПеречисления ИЗ Перечисления.Аид_ТипыСобытийОповещений.ИзменениеСостоянияЗаказа.Метаданные().ЗначенияПеречисления Цикл
		Если НЕ Перечисления.Аид_ТипыСобытийОповещений.ДанныеДляОбработкиОчередиОповещений(Перечисления.Аид_ТипыСобытийОповещений[ЗначениеПеречисления.Имя]).ДоступноДляИспользования Тогда
			Продолжить;
		КонецЕсли;
		МассивНазначенийШаблона.Добавить(ЗначениеПеречисления.Имя);
	КонецЦикла;
	
	НазначениеШаблонаЭтоОповещение = Ложь;
	Если МассивНазначенийШаблона.Найти(НазначениеШаблона) <> Неопределено Тогда
		НазначениеШаблонаЭтоОповещение = Истина;
		ИмяМакета = СтрЗаменить(НазначениеШаблона, "Перечисление.Аид_ТипыСобытийОповещений.", "");
	КонецЕсли;
	
	Если НазначениеШаблонаЭтоОповещение Тогда 
		Аид_ШаблоныСообщений.СформироватьСписокРеквизитовПоСКД(Реквизиты, Перечисления.Аид_ТипыСобытийОповещений.ПолучитьМакет(ИмяМакета));
	КонецЕсли;
	
	Аид_ШаблоныСообщенийЛокализация.ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, НазначениеШаблона, ДополнительныеПараметры);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Вызывается в момент создания сообщений по шаблону для заполнения значений реквизитов и вложений.
//
// Параметры:
//  Сообщение - Структура:
//    * ЗначенияРеквизитов - Соответствие - список используемых в шаблоне реквизитов:
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * ЗначенияОбщихРеквизитов - Соответствие - список используемых в шаблоне общих реквизитов:
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * Вложения - Соответствие:
//      ** Ключ     - Строка - имя вложения в шаблоне;
//      ** Значение - ДвоичныеДанные
//                  - Строка - двоичные данные или адрес во временном хранилище вложения.
//    * ДополнительныеПараметры - Структура - дополнительные параметры сообщения. 
//  НазначениеШаблона - Строка -  полное имя назначения шаблон сообщения.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//  ПараметрыШаблона - Структура -  дополнительная информация о шаблоне сообщения.
//
Процедура ПриФормированииСообщения(Сообщение, НазначениеШаблона, ПредметСообщения, ПараметрыШаблона) Экспорт
	
	//++ НЕ ГОСИС
	МассивНазначенийШаблона = Новый Массив;
	Для Каждого ЗначениеПеречисления ИЗ Перечисления.Аид_ТипыСобытийОповещений.ИзменениеСостоянияЗаказа.Метаданные().ЗначенияПеречисления Цикл
		Если НЕ Перечисления.Аид_ТипыСобытийОповещений.ДанныеДляОбработкиОчередиОповещений(Перечисления.Аид_ТипыСобытийОповещений[ЗначениеПеречисления.Имя]).ДоступноДляИспользования Тогда
			Продолжить;
		КонецЕсли;
		МассивНазначенийШаблона.Добавить(ЗначениеПеречисления.Имя);
	КонецЦикла;
	
	Если МассивНазначенийШаблона.Найти(НазначениеШаблона) <> Неопределено Тогда
		Аид_ШаблоныСообщений.ЗаполнитьРеквизитыПоСКД(Сообщение.ЗначенияРеквизитов, ПредметСообщения, ПараметрыШаблона);
	КонецЕсли;
	
	Аид_ШаблоныСообщенийЛокализация.ПриФормированииСообщения(Сообщение, НазначениеШаблона, ПредметСообщения, ПараметрыШаблона);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Заполняет список получателей SMS при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиSMS - ТаблицаЗначений:
//     * НомерТелефона - Строка - номер телефона, куда будет отправлено сообщение SMS;
//     * Представление - Строка - представление получателя сообщения SMS;
//     * Контакт       - Произвольный - контакт, которому принадлежит номер телефона.
//  НазначениеШаблона - Строка - идентификатор назначения шаблона
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект, являющийся источником данных.
//                   - Структура  - структура описывающая параметры шаблона:
//    * Предмет               - ЛюбаяСсылка - ссылка на объект, являющийся источником данных;
//    * ВидСообщения - Строка - вид формируемого сообщения: "ЭлектроннаяПочта" или "СообщениеSMS";
//    * ПроизвольныеПараметры - Соответствие - заполненный список произвольных параметров;
//    * ОтправитьСразу - Булево - признак мгновенной отправки;
//    * ПараметрыСообщения - Структура - дополнительные параметры сообщения.
//
Процедура ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, НазначениеШаблона, ПредметСообщения) Экспорт
	
	//++ НЕ ГОСИС
	Аид_ФормированиеПечатныхФорм.ЗаполнитьПолучателейСообщенияПоШаблону(ПолучателиSMS, НазначениеШаблона, ПредметСообщения, Истина);
	
	Аид_ШаблоныСообщенийЛокализация.ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, НазначениеШаблона, ПредметСообщения);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Заполняет список получателей почты при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиПисьма - ТаблицаЗначений - список получается письма:
//     * Адрес           - Строка - адрес электронной почты получателя;
//     * Представление   - Строка - представление получателя письма;
//     * Контакт         - Произвольный - контакт, которому принадлежит адрес электронной почты.
//  НазначениеШаблона - Строка - идентификатор назначения шаблона.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект, являющийся источником данных.
//                   - Структура  - структура описывающая параметры шаблона:
//    * Предмет               - ЛюбаяСсылка - ссылка на объект, являющийся источником данных;
//    * ВидСообщения - Строка - вид формируемого сообщения: "ЭлектроннаяПочта" или "СообщениеSMS";
//    * ПроизвольныеПараметры - Соответствие - заполненный список произвольных параметров;
//    * ОтправитьСразу - Булево - признак мгновенной отправки письма;
//    * ПараметрыСообщения - Структура - дополнительные параметры сообщения;
//    * ПреобразовыватьHTMLДляФорматированногоДокумента - Булево - признак преобразование HTML текста
//             сообщения содержащего картинки в тексте письма из-за особенностей вывода изображений
//             в форматированном документе;
//    * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись для отправки письма.
//
Процедура ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, НазначениеШаблона, ПредметСообщения) Экспорт
	
	//++ НЕ ГОСИС
	Аид_ФормированиеПечатныхФорм.ЗаполнитьПолучателейСообщенияПоШаблону(ПолучателиПисьма, НазначениеШаблона, ПредметСообщения, Ложь);
	
	Аид_ШаблоныСообщенийЛокализация.ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, НазначениеШаблона, ПредметСообщения);
	//-- НЕ ГОСИС
	
КонецПроцедуры

#КонецОбласти

